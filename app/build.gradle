apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

// Manifest version information!
def versionMajor = 0
def versionMinor = 9
def versionBuild = 0 // bump for dogfood builds, public betas, etc.
static def currentTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00"))
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.wander.movie"
        minSdkVersion 21
        targetSdkVersion 29
        multiDexEnabled true
        versionCode versionMajor * 100 + versionMinor * 10 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "movie"]

        ndk {
            // 设置支持的SO库架构
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
        resConfigs 'zh'
    }

    signingConfigs {
        debug_sign {
            storeFile file("wander.jks")
            keyAlias keystoreProperties['keyAlias']
            Properties properties = new Properties()
            properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
            storePassword = properties.getProperty("KEYSTORE_PASSWORD")
            keyPassword = properties.getProperty("KEY_PASSWORD")
        }

        release_sign {
            storeFile file("wander.jks")
            keyAlias keystoreProperties['keyAlias']
            Properties properties = new Properties()
            properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
            storePassword = properties.getProperty("KEYSTORE_PASSWORD")
            keyPassword = properties.getProperty("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release_sign
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release_sign
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //修改生成的apk名字
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "debug") {
                outputFileName = "watch_movie-${variant.buildType.name}-${variant.versionName}-debug.apk"
            } else {
                def buildTime = currentTime()
                //outputFileName = "Reader-Android-${variant.buildType.name}-${variant.versionName}-release.apk"
                outputFileName = "watch_movie-${variant.buildType.name}-${variant.versionName}-${buildTime}.apk"
            }
        }
    }


    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }


    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
        ignoreWarnings true

        disable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'
    implementation project(path: ':baseframe')
    implementation 'com.lzy.net:okgo:3.0.4'

}
